unit TestmRouter;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit 
  being tested.

}

interface

uses
  mRouter,
  TestFramework, System.SysUtils, System.Generics.Collections,
  System.Classes, System.Rtti;

type
  // Test methods for class TRouter

  TestTRouter = class(TTestCase)
  strict private
    FRouter: TRouter<Integer>;
  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
    procedure TestNotify;
    procedure TestNotifyWithData;
    procedure TestWithDatas;
    procedure TestRemoveHandler;
    procedure TestERouterMethodIDAlreadyExists;
    procedure TestExcute;
    procedure TestExcuteWithData;
    procedure TestData;
    procedure TestQueryResultObj;
    procedure TestQueryVarObj;
  end;

implementation

type
  TObj = class
    Excute: Boolean;
    constructor Create;
  end;

{ TObj }

constructor TObj.Create;
begin
  Excute := False;
end;

procedure TestTRouter.SetUp;
begin
  FRouter := TRouter<Integer>.Create;
  FRouter.Clear;
end;

procedure TestTRouter.TearDown;
begin
  FRouter.Free;
  FRouter := nil;
end;

procedure TestTRouter.TestNotify;
var
  LExcute: Boolean;
begin
  LExcute := False;
  FRouter.Handler(0,
    procedure
    begin
      LExcute := True
    end);
  FRouter.Notify(0);
  CheckTrue(LExcute);
end;

procedure TestTRouter.TestNotifyWithData;
var
  Data: TObj;
begin
  Data := TObj.Create;
  try
    FRouter.Handler(0,
      procedure
      begin
        FRouter.Data<TObj>.Excute := True;
      end);
    CheckFalse(Data.Excute);
    FRouter.Notify(0, Data);
    CheckTrue(Data.Excute);
  finally
    Data.Free;
  end;
end;

procedure TestTRouter.TestWithDatas;
var
  Data, Data2: TObj;
begin
  FRouter.Handler(0,
    procedure
    begin
        FRouter.Data<TObj>.Excute := True;
        FRouter.Data2<TObj>.Excute := True;
    end);
  Data := TObj.Create;
  Data2 := TObj.Create;
  try
    FRouter.Notify(0, Data, Data2);
    CheckTrue(Data.Excute and Data2.Excute);
  finally
    Data.Free;
    Data2.Free;
  end;
end;

procedure TestTRouter.TestRemoveHandler;
var
  LExcute: Boolean;
  Proc: TProc;
begin
  LExcute := False;
  Proc :=
    procedure
    begin
      LExcute := True
    end;
  FRouter.Handler(0, Proc);
  FRouter.Notify(0);
  CheckTrue(LExcute);

  LExcute := False;
  FRouter.RemoveHandler(0, Proc);
  CheckFalse(LExcute);
end;

procedure TestTRouter.TestERouterMethodIDAlreadyExists;
begin
  ExpectedException := ERouterMethodIDAlreadyExists;
  FRouter.Handler(0, function: Boolean begin Result := True end);
  FRouter.Handler(0, function: Boolean begin Result := False end);
end;

procedure TestTRouter.TestExcute;
var
  LExcute: Boolean;
begin
  FRouter.Handler(0, function: Boolean begin Result := True end);
  LExcute := False;
  CheckFalse(LExcute);
  LExcute := FRouter.Excute(0);
  CheckTrue(LExcute);

  FRouter.RemoveHandler(0);
  FRouter.Handler(0, function: Boolean begin Result := False end);
  LExcute := FRouter.Excute(0);
  CheckFalse(LExcute);
end;

procedure TestTRouter.TestExcuteWithData;
var
  Data: TObj;
begin
  Data := TObj.Create;
  try
    FRouter.Handler(0,
      function: Boolean
      begin
        Result := True;
        FRouter.Data<TObj>.Excute := Result;
      end);
    CheckFalse(Data.Excute);
    CheckTrue(FRouter.Excute(0, Data));
    CheckTrue(Data.Excute);

    FRouter.RemoveHandler(0);
    FRouter.Handler(0,
      function: Boolean
      begin
        Result := False;
        FRouter.Data<TObj>.Excute := Result;
      end);
    CheckFalse(FRouter.Excute(0, Data));
    CheckFalse(Data.Excute);
  finally
    Data.Free;
  end;
end;

procedure TestTRouter.TestData;
var
  LObj2, LObj: TObj;
begin
  FRouter.Data<TObj>(TObj.Create);
  LObj := FRouter.Data<TObj>;
  CheckFalse(LObj.Excute);
  FRouter.Data<TObj>.Excute := True;
  CheckTrue(LObj.Excute);

  FRouter.Data2<TObj>(TObj.Create);
  LObj2 := FRouter.Data2<TObj>;
  CheckFalse(LObj2.Excute);
  FRouter.Data2<TObj>.Excute := True;
  CheckTrue(LObj2.Excute);
end;

procedure TestTRouter.TestQueryResultObj;
var
  LObj: TObj;
begin
  FRouter.Feed<TObj>(0,
    function: TObj
    begin
      Result := TObj.Create;
      Result.Excute := True;
    end);
  LObj := FRouter.Query<TObj>(0);
  CheckNotNull(LObj);
  CheckTrue(LObj.Excute);
end;

procedure TestTRouter.TestQueryVarObj;
var
  LObj: TObj;
begin
  CheckFalse(FRouter.Query<TObj>(0, LObj));
  FRouter.Feed<TObj>(0,
    function: TObj
    begin
      Result := TObj.Create;
      Result.Excute := True;
    end);
  CheckTrue(FRouter.Query<TObj>(0, LObj));
  CheckNotNull(LObj);
  CheckTrue(LObj.Excute);
end;


initialization
  // Register any test cases with the test runner
  RegisterTest(TestTRouter.Suite);

end.
